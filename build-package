#!/usr/bin/perl -w
use strict;
use Cwd;
use IPC::Run3;
use File::Copy;

my $NAME='Jesse J. Cook';
my $EMAIL='jcook@camber.com';
my $DCHENV="DEBFULLNAME=\"$NAME\" DEBEMAIL=\"$EMAIL\"";
my $PATCHES=getcwd;

my $source_path = "";
my $dh;

mkdir "build" or die $!;
chdir "build" or die $!;

# Pull source
my @args = ("apt-get", "source", "libvirt");
0 == system(@args) or die "system '@args' failed: $?";

opendir($dh, '.') or die $!;
while(readdir $dh) {
    if($_ =~ /^libvirt-/) {
        $source_path = $_;
        last;
    }
}
closedir $dh;

if("" eq $source_path) {
    die "Source directory not found";
}
chdir $source_path;

# Apply patches
my $count = 0;
opendir($dh, $PATCHES) or die $!;
while(readdir $dh) {
    next unless $_ =~ /\.patch$/;

    my $patch_file = $_;
    my $patch_subject = "";
    my $patch_body = "";

    copy("$PATCHES/$patch_file", "$patch_file");
    $patch_file = getcwd . "/$patch_file";
    open FILE, "<$patch_file" or die $!;
    while(<FILE>) {
        next unless $_ =~ /^Subject:/;
        ($patch_subject = $_) =~ s/^Subject:\s*//;
        last;
    }
    while(<FILE>) {
        last if $_ =~ /^---\n$/;
        $patch_body .= $_;
    }
    close FILE;

    print $patch_subject;
    print $patch_body;

    @args = ("patch", "-p1"); # pass patch_file in via stdin
    run3 \@args, "$patch_file" or die $!;
    0 == $? or die "'@args' returned non-zero return code";

    my $patch_name = "";
    ($patch_name = $patch_subject) =~ s/\r|\n//g;
    ($patch_name = $patch_name) =~ s/ /-/g;
    @args = ("dpkg-source", "--commit", "--auto-commit",
        ".", "$patch_name", "$patch_file");
    0 == system(@args) or die "'@args' returned non-zero return code";
}
closedir $dh;

# Build package
@args = ("debuild", "-us", "-uc");
0 == system(@args) or die "'@args' returned non-zero return code";

exit 0;
